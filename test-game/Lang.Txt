; Lang.Txt for Precious Days
;;;TODO: Tell people what this file is about

; --------------------------- Special Verbs ------------------------------- ;
;
; Verbs which perform special functions (note - .start is at the end of the
; file, because it requires several Daemons...)

.travel			; Check if player can move before processing TT..
syntax=verb any
    reply "And, moving..."

.speech
syntax=verb text player
	syntax noun2 noun1
syntax=verb player text
	tell player '@xy @vbs you "@n2"'
	respond 'You @vb @n1 "@n2"'
syntax=verb text
	announce others '@xx says "@n1"'
	reply 'OK, @me says "@n1"'

; ---------------------------- Travel Verbs -------------------------------- ;
;
;  This little bunch are the ones you need to move around. I've used the
;  FULL names here and in Travel.Txt so that when a user requests an EXITS
;  list the FULL names are given, which looks MUCH tidier.
;

travel=north south east west northeast southeast southwest northwest up down in out jump


left
syntax=verb any
	treatas west


right
syntax=verb any
	treatas east


; dream: can be done while sleeping
verb=quit dream
syntax=verb any
	quit


; repeat the room description or inspect something
verb=look
syntax=verb none	; "look" on its own describes current room
	-> look			; call 'look' and then endparse.
syntax=verb noun	; Looking AT something.
	checknear noun			; gotta be near the thing
    ;;;TODO: Describe
    respond "It hasn't changed."


verb=examine
syntax=verb noun
	treatas look


verb=who
syntax=verb any
	who verbose

verb=qw	
syntax=verb any
	reply "Current users: {"
	who b


; force save score
verb=save
syntax=verb none
	save


; report curent score, verbose
verb=score
syntax=verb none
	score v


; report current score, brief
verb=qs
syntax=verb none
	score b


; report exits
verb=exits
syntax=verb
	reply "Exits:"
	exits

; toggle auto-description of exits
verb=autoexits
syntax=verb none
    if autoexits then autoexits off
    & respond "Auto-exits are now off."
    else autoexits on
    & respond "Auto-exits are now on."

verb=inventory
syntax=verb
	invent


; Brief mode - short descriptions only (can do while asleep)
verb=brief dream
syntax=verb any
	rdmode b
	reply "You will be given brief descriptions of each room now; use Look to see the long description of a room."

; verbose descriptions (always give full description)
verb=verbose dream
syntax=verb any
	rdmode v
	reply "You will be given long desrciptions of each room now."

verb=roomcount dream
syntax=verb any
	rdmode r
	reply "The long description of each room will only be given the first time you visit a room, to see the long description again use Look."


; report value of object
verb=value
syntax=verb noun
	;noun1 all error "No support for 'all' yet."
	checknear noun		; Must be near it.
	reply "@n1 currently worth: @v1 point(s)."


; report weight of object
verb=weigh
syntax=verb noun
	;noun1 all error "No support for 'all' yet."
	checknear noun
	reply "@n1 currently weighs around @w1g."


; '?hcae' means find objects here before carried
verb=get ?hcae
syntax=verb verb=up noun
	syntax noun none
syntax=verb none
	error "@vb what?"
syntax=verb noun
	;noun1 all error "No support for 'all' yet."
	checknear noun
	checkget noun		; Check that we can pick it up.
    get noun


verb=drop
syntax=verb none
	error "@vb what?"
syntax=verb noun
	;noun1 all error NoAllYet
    if not carrying noun error "You aren't carrying the @n1."
	reply "@n1 dropped."
    drop noun
    action others "@xx has dropped the @n1."


verb=light	1chae 0chae
    ;; intended syntax is: light X from Y
syntax=verb noun
	checknear noun
	error "From what?"
syntax=verb noun noun
	checknear noun
	checknear noun2
	lit noun1	respond "The @n1 is already alight."
	!lit noun2	error "The @n2 isn't burning, so you can't light anything else from it."
	!burns noun1	respond "You can't light that."
	reply "The @n1 catches the flame and is now alight."
	fire noun	burn noun
	else		state noun 0
	!carrying noun1 action others "@xx has set the @n1 alight."
	actionfrom noun "@xx has lit the @n1."


verb=what
syntax=verb
	what

verb=locname
syntax=verb
	whereami

; Magic

verb=summon
syntax=verb
	respond "Summon WHO?"
syntax=verb noun
	carrying noun	respond "You've already got one."
	nearto noun	error "It's already here."
	error "Go fetch it yourself! Lazy bones."
syntax=verb player=me
	error "You'll have to ASK someone else to do that."
syntax=verb player
	cansee player	respond "@n1 is already here."
	action cansee "@me begins to cast a spell..."
	tell player "The scene around you begins to fade... {"
	summon player
	respond "Your spell succeeded."


verb=force
syntax=verb player=me text
	respond "No will power, eh?"
syntax=player text
	reply "Ok."
	force player text
syntax=text player
	syntax player text

verb=reset
syntax=verb none
	respond "Next reset is due in @re."

verb=go
syntax=verb verb
	treatas noun1

verb=pi
syntax=verb
	respond "3.14159, or thereabouts."
syntax=verb noun
	treatas get


;; Limbo travel flag daemons
verb=.limbo_nw
syntax=any
    endparse

verb=.limbo_s
syntax=any
    endparse
